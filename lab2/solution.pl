% ---------------------------------
% Предикаты для работы со списками

% Базовый случай: предикат истинный для всех элементов пустого списка.
all_true(_, [], _).

% Рекурсивный случай: Проверяем предикат для головы списка, затем рекурсивно для хвоста.
all_true(Pred, [X|Xs], Arg) :-
    call(Pred, X, Arg),       % Вызываем предикат Pred для элемента X.
    all_true(Pred, Xs, Arg).  % Рекурсивно проверяем для оставшегося списка.

zip([], [], []).  % Базовый случай: пустые списки -> пустой результат
zip([X|Xs], [Y|Ys], [[X, Y]|Zs]) :-
    zip(Xs, Ys, Zs).  % Рекурсия по спискам    
% ---------------------------------


% ---------------------------------
% Следующие предикаты по богу, его фразе и реальному расположению богов проверяет, нет ли противоречий (например Бог Правды солгал или Бог Лжи был честен).
compare_god_and_phrase(truth, [], []).
compare_god_and_phrase(truth, [X | Phrase], [Y | Real]) :-
    X = Y,
    compare_god_and_phrase(truth, Phrase, Real).

compare_god_and_phrase(lies, [], []).
compare_god_and_phrase(lies, [X | Phrase], [Y | Real]) :-
    \+ var(X), \+ var(Y), !,  % Эти танцы с бубном нужны для случая с анонимной переменной (когда бог не упоминал некоторую позицию и там может быть что угодно)
    X \= Y,
    compare_god_and_phrase(lies, Phrase, Real).
compare_god_and_phrase(lies, [_ | Phrase], [_ | Real]) :-
    compare_god_and_phrase(lies, Phrase, Real).

compare_god_and_phrase(diplomacy, _, _).
% ---------------------------------


% ---------------------------------
% Этот предикат проверяет, совпадает ли размещение богов со сказанными ими фразами.
% Например, если список богов [Правда, Ложь, Дипломатия], и бог правды сказал, что размещение богов [_, Правда, _],
%  то мы получим противоречие, потому что Бог Правды солгал
contradiction(Gods, Phrases) :-
    permutation([truth, lies, diplomacy], Gods),
    zip(Gods, Phrases, GodsPhrases),
    all_true(check, GodsPhrases, Gods).

check([God, Phrase], Gods) :-
    compare_god_and_phrase(God, Phrase, Gods).
% ---------------------------------


% Немного слов о моём решении.
% Сложность его порядка O(N!), где N - количество богов (если бы боги могли повторяться).
% Интуитивно понятно, что моё решение находит правильный ответ, потому что
%   мы просто перебираем все варианты размещения богов по статуям, а затем проверяем,
%   подходит ли данное размещение под сказанные ими фразы или нет.
